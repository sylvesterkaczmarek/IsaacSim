# SPDX-FileCopyrightText: Copyright (c) 2026 NVIDIA CORPORATION & AFFILIATES. All rights reserved.
# SPDX-License-Identifier: Apache-2.0
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

include_guard(GLOBAL)

set(_ISAACSIM_DOCTEST_MAIN_SOURCE "${ISAACSIM_LIBRARIES_DIR}/testing/cpp/DoctestMain.cpp")

function(_isaacsim_validate_test_sources module_name language_directory extension)
    foreach(source IN LISTS ARGN)
        _isaacsim_validate_relative_path("${module_name} ${language_directory} test source" "${source}")
        cmake_path(NORMAL_PATH source OUTPUT_VARIABLE normalized_source)
        if(NOT normalized_source MATCHES "^tests/${language_directory}/.+\\.${extension}$")
            message(FATAL_ERROR
                "${module_name} ${language_directory} test sources must be .${extension} files under "
                "tests/${language_directory}: ${source}"
            )
        endif()
        cmake_path(GET normalized_source FILENAME source_filename)
        string(TOLOWER "${source_filename}" source_filename_lower)
        if(source_filename_lower STREQUAL "main.cpp")
            message(FATAL_ERROR
                "${module_name} ${language_directory} test sources must not provide Main.cpp; "
                "the native test helper supplies the shared doctest entry point"
            )
        endif()
        if(NOT EXISTS "${CMAKE_CURRENT_SOURCE_DIR}/${normalized_source}")
            message(FATAL_ERROR "${module_name} test source does not exist: ${source}")
        endif()
    endforeach()
endfunction()

function(isaacsim_add_c_tests)
    set(one_value_args MODULE RESOURCE_DIR)
    set(multi_value_args SOURCES DEPENDENCIES LABELS)
    cmake_parse_arguments(ARG "" "${one_value_args}" "${multi_value_args}" ${ARGN})
    if(ARG_UNPARSED_ARGUMENTS)
        message(FATAL_ERROR "isaacsim_add_c_tests received unknown arguments: ${ARG_UNPARSED_ARGUMENTS}")
    endif()
    if(NOT ARG_MODULE OR NOT ARG_SOURCES)
        message(FATAL_ERROR "isaacsim_add_c_tests requires MODULE and SOURCES")
    endif()
    _isaacsim_validate_test_sources("${ARG_MODULE}" c cpp ${ARG_SOURCES})
    if(NOT BUILD_TESTING)
        return()
    endif()

    _isaacsim_get_module_names("${ARG_MODULE}" module_target module_alias module_path module_output_dir)
    set(test_target "tests-c-${module_target}")
    add_executable(${test_target} "${_ISAACSIM_DOCTEST_MAIN_SOURCE}" ${ARG_SOURCES})
    _isaacsim_apply_build_options(${test_target})
    target_compile_features(${test_target} PRIVATE cxx_std_17)
    target_link_libraries(${test_target} PRIVATE ${module_target} doctest::doctest ${ARG_DEPENDENCIES})
    target_include_directories(${test_target} PRIVATE "${ISAACSIM_LIBRARIES_DIR}/testing/cpp")
    set_target_properties(${test_target} PROPERTIES
        RUNTIME_OUTPUT_DIRECTORY "${module_output_dir}/tests"
        BUILD_RPATH "${module_output_dir}/lib"
    )
    if(WIN32)
        # Resolved at generate time, so it sees linked dependencies whose add_subdirectory() runs
        # after this one -- a configure-time target walk silently drops those and the test then
        # fails to launch with STATUS_DLL_NOT_FOUND.
        add_custom_command(TARGET ${test_target} POST_BUILD
            COMMAND ${CMAKE_COMMAND} -E copy_if_different
                "$<TARGET_RUNTIME_DLLS:${test_target}>"
                "$<TARGET_FILE_DIR:${test_target}>"
            COMMAND_EXPAND_LISTS
        )
        # Runtime-only plugins are not linked, so TARGET_RUNTIME_DLLS cannot see them.
        get_target_property(external_test_runtime_targets
            ${module_target} ISAACSIM_EXTERNAL_RUNTIME_TARGETS)
        if(external_test_runtime_targets
           AND NOT external_test_runtime_targets MATCHES "-NOTFOUND$")
            list(REMOVE_DUPLICATES external_test_runtime_targets)
            foreach(runtime_target IN LISTS external_test_runtime_targets)
                add_custom_command(TARGET ${test_target} POST_BUILD
                    COMMAND ${CMAKE_COMMAND} -E copy_if_different
                        "$<TARGET_FILE:${runtime_target}>"
                        "$<TARGET_FILE_DIR:${test_target}>/$<TARGET_FILE_NAME:${runtime_target}>"
                    VERBATIM
                )
            endforeach()
        endif()
    endif()
    add_test(NAME ${test_target} COMMAND ${test_target})
    set_tests_properties(${test_target} PROPERTIES LABELS "unit;c;${ARG_MODULE};${ARG_LABELS}")
    if(WIN32)
        set_property(TEST ${test_target} APPEND PROPERTY
            ENVIRONMENT_MODIFICATION "PATH=path_list_prepend:${module_output_dir}/lib"
        )
    endif()
    if(ARG_RESOURCE_DIR)
        set(resource_working_directory "${ARG_RESOURCE_DIR}")
        if(NOT IS_ABSOLUTE "${resource_working_directory}")
            set(resource_working_directory "${CMAKE_CURRENT_SOURCE_DIR}/${resource_working_directory}")
        endif()
        if(NOT EXISTS "${resource_working_directory}/TEST_RESOURCES.md")
            message(FATAL_ERROR
                "${ARG_MODULE} test resource directory must contain TEST_RESOURCES.md: ${resource_working_directory}"
            )
        endif()
        set_tests_properties(${test_target} PROPERTIES WORKING_DIRECTORY "${resource_working_directory}")
    endif()
endfunction()

function(isaacsim_add_cpp_tests)
    set(options REQUIRES_NO_BINDINGS)
    set(one_value_args MODULE RESOURCE_DIR)
    set(multi_value_args SOURCES DEPENDENCIES LABELS)
    cmake_parse_arguments(ARG "${options}" "${one_value_args}" "${multi_value_args}" ${ARGN})
    if(ARG_UNPARSED_ARGUMENTS)
        message(FATAL_ERROR "isaacsim_add_cpp_tests received unknown arguments: ${ARG_UNPARSED_ARGUMENTS}")
    endif()
    if(NOT ARG_MODULE OR NOT ARG_SOURCES)
        message(FATAL_ERROR "isaacsim_add_cpp_tests requires MODULE and SOURCES")
    endif()
    _isaacsim_validate_test_sources("${ARG_MODULE}" cpp cpp ${ARG_SOURCES})
    if(NOT BUILD_TESTING)
        return()
    endif()
    if(ARG_REQUIRES_NO_BINDINGS AND ISAACSIM_ENABLE_PYTHON_BINDINGS)
        return()
    endif()

    _isaacsim_get_module_names("${ARG_MODULE}" module_target module_alias module_path module_output_dir)
    set(test_target "tests-${module_target}")
    add_executable(${test_target} "${_ISAACSIM_DOCTEST_MAIN_SOURCE}" ${ARG_SOURCES})
    _isaacsim_apply_build_options(${test_target})
    target_link_libraries(${test_target} PRIVATE ${module_target} doctest::doctest ${ARG_DEPENDENCIES})
    target_include_directories(${test_target} PRIVATE "${ISAACSIM_LIBRARIES_DIR}/testing/cpp")
    set_target_properties(${test_target} PROPERTIES
        RUNTIME_OUTPUT_DIRECTORY "${module_output_dir}/tests"
        BUILD_RPATH "${module_output_dir}/lib"
    )
    if(WIN32)
        # Resolved at generate time, so it sees linked dependencies whose add_subdirectory() runs
        # after this one -- a configure-time target walk silently drops those and the test then
        # fails to launch with STATUS_DLL_NOT_FOUND.
        add_custom_command(TARGET ${test_target} POST_BUILD
            COMMAND ${CMAKE_COMMAND} -E copy_if_different
                "$<TARGET_RUNTIME_DLLS:${test_target}>"
                "$<TARGET_FILE_DIR:${test_target}>"
            COMMAND_EXPAND_LISTS
        )
        # Runtime-only plugins are not linked, so TARGET_RUNTIME_DLLS cannot see them.
        get_target_property(external_test_runtime_targets
            ${module_target} ISAACSIM_EXTERNAL_RUNTIME_TARGETS)
        if(external_test_runtime_targets
           AND NOT external_test_runtime_targets MATCHES "-NOTFOUND$")
            list(REMOVE_DUPLICATES external_test_runtime_targets)
            foreach(runtime_target IN LISTS external_test_runtime_targets)
                add_custom_command(TARGET ${test_target} POST_BUILD
                    COMMAND ${CMAKE_COMMAND} -E copy_if_different
                        "$<TARGET_FILE:${runtime_target}>"
                        "$<TARGET_FILE_DIR:${test_target}>/$<TARGET_FILE_NAME:${runtime_target}>"
                    VERBATIM
                )
            endforeach()
        endif()
    endif()
    add_test(NAME ${test_target} COMMAND ${test_target})
    set_tests_properties(${test_target} PROPERTIES LABELS "unit;cpp;${ARG_MODULE};${ARG_LABELS}")
    if(WIN32)
        set_property(TEST ${test_target} APPEND PROPERTY
            ENVIRONMENT_MODIFICATION "PATH=path_list_prepend:${module_output_dir}/lib"
        )
    endif()
    if(ARG_RESOURCE_DIR)
        set(resource_working_directory "${ARG_RESOURCE_DIR}")
        if(NOT IS_ABSOLUTE "${resource_working_directory}")
            set(resource_working_directory "${CMAKE_CURRENT_SOURCE_DIR}/${resource_working_directory}")
        endif()
        if(NOT EXISTS "${resource_working_directory}/TEST_RESOURCES.md")
            message(FATAL_ERROR
                "${ARG_MODULE} test resource directory must contain TEST_RESOURCES.md: ${resource_working_directory}"
            )
        endif()
        set_tests_properties(${test_target} PROPERTIES WORKING_DIRECTORY "${resource_working_directory}")
    endif()
endfunction()

function(isaacsim_add_python_tests)
    set(options REQUIRES_BINDINGS)
    set(one_value_args MODULE RESOURCE_DIR TIMEOUT)
    # TEST_SUPPORT: sibling module names whose staged tests/ dir holds shared test
    # support (scenarios, runner, _physics_setup) this module's tests import. Their
    # staged test dirs are added to the pytest PYTHONPATH.
    set(multi_value_args LABELS TEST_SUPPORT)
    cmake_parse_arguments(ARG "${options}" "${one_value_args}" "${multi_value_args}" ${ARGN})
    if(ARG_UNPARSED_ARGUMENTS)
        message(FATAL_ERROR "isaacsim_add_python_tests received unknown arguments: ${ARG_UNPARSED_ARGUMENTS}")
    endif()
    if(NOT ARG_MODULE)
        message(FATAL_ERROR "isaacsim_add_python_tests requires MODULE")
    endif()
    if(ARG_TIMEOUT AND (NOT ARG_TIMEOUT MATCHES "^[1-9][0-9]*$"))
        message(FATAL_ERROR "isaacsim_add_python_tests TIMEOUT must be a positive integer")
    endif()
    if(NOT BUILD_TESTING OR NOT ISAACSIM_ENABLE_PYTHON)
        return()
    endif()
    if(ARG_REQUIRES_BINDINGS AND NOT ISAACSIM_ENABLE_PYTHON_BINDINGS)
        return()
    endif()

    _isaacsim_get_module_names("${ARG_MODULE}" module_target module_alias module_path module_output_dir)
    if(NOT TARGET "stage-${module_target}-python-package")
        message(FATAL_ERROR "isaacsim_add_python_tests requires a registered Python module: ${ARG_MODULE}")
    endif()
    set(source_test_dir "${CMAKE_CURRENT_SOURCE_DIR}/tests/python")
    if(NOT EXISTS "${source_test_dir}")
        message(FATAL_ERROR "${ARG_MODULE} Python test directory does not exist: ${source_test_dir}")
    endif()
    set(resource_root "${CMAKE_CURRENT_SOURCE_DIR}")
    if(ARG_RESOURCE_DIR)
        set(resource_root "${ARG_RESOURCE_DIR}")
        if(NOT IS_ABSOLUTE "${resource_root}")
            set(resource_root "${CMAKE_CURRENT_SOURCE_DIR}/${resource_root}")
        endif()
        if(NOT EXISTS "${resource_root}/TEST_RESOURCES.md")
            message(FATAL_ERROR
                "${ARG_MODULE} test resource directory must contain TEST_RESOURCES.md: ${resource_root}"
            )
        endif()
    endif()
    _isaacsim_get_python_stage_dir(python_stage_dir)
    set(staged_test_dir "${CMAKE_BINARY_DIR}/python-tests/${module_path}")
    set(shared_testing_dir "${CMAKE_CURRENT_FUNCTION_LIST_DIR}/../testing/python")
    file(GLOB_RECURSE python_test_sources CONFIGURE_DEPENDS LIST_DIRECTORIES FALSE "${source_test_dir}/*")
    file(GLOB_RECURSE shared_testing_sources CONFIGURE_DEPENDS LIST_DIRECTORIES FALSE "${shared_testing_dir}/*.py")
    set(python_package_stage_target "stage-${module_target}-python-package")
    get_target_property(python_package_stage_stamp
        ${python_package_stage_target} ISAACSIM_PYTHON_STAGE_STAMP
    )
    set(test_stage_stamp "${module_output_dir}/python-tests-stage.stamp")
    add_custom_command(
        OUTPUT "${test_stage_stamp}"
        COMMAND ${CMAKE_COMMAND} -E make_directory "${module_output_dir}"
        COMMAND ${CMAKE_COMMAND}
            "-DSOURCE=${source_test_dir}"
            "-DDESTINATION=${staged_test_dir}/tests"
            -P "${_ISAACSIM_CMAKE_HELPER_DIR}/StageEditableDirectory.cmake"
        COMMAND ${CMAKE_COMMAND} -E copy_if_different
            ${shared_testing_sources}
            "${staged_test_dir}"
        COMMAND ${CMAKE_COMMAND} -E touch "${test_stage_stamp}"
        DEPENDS
            ${python_test_sources}
            ${shared_testing_sources}
            ${python_package_stage_target}
            "${python_package_stage_stamp}"
            "${_ISAACSIM_CMAKE_HELPER_DIR}/StageEditableDirectory.cmake"
        VERBATIM
    )
    add_custom_target("stage-${module_target}-python-tests" ALL DEPENDS "${test_stage_stamp}")
    # Shared test-support dirs (TEST_SUPPORT modules' staged tests) go after this
    # module's own tests but before the deps, so a module's own package wins on name
    # collisions (e.g. each has its own `common`).
    set(support_test_paths "")
    foreach(support_module IN LISTS ARG_TEST_SUPPORT)
        _isaacsim_get_module_names("${support_module}" _support_target _support_alias support_module_path _support_dir)
        string(APPEND support_test_paths "${CMAKE_BINARY_DIR}/python-tests/${support_module_path}/tests;")
    endforeach()
    set(python_test_paths
        "${python_stage_dir};${support_test_paths}${ISAACSIM_NATIVE_RUNTIME_DEPS_DIR};${ISAACSIM_PYTHON_RUNTIME_DEPS_DIR};${ISAACSIM_PYTHON_TEST_DEPS_DIR}"
    )
    set(python_test_environment_modifications)
    foreach(python_test_path IN LISTS python_test_paths)
        list(APPEND python_test_environment_modifications
            "PYTHONPATH=path_list_append:${python_test_path}"
        )
    endforeach()
    set(test_name "tests-python-${module_target}")
    add_test(NAME ${test_name}
        COMMAND ${Python_EXECUTABLE} -m pytest
            "${staged_test_dir}"
            "--rootdir=${staged_test_dir}"
            -p no:cacheprovider
            -v
    )
    set_tests_properties(${test_name} PROPERTIES
        ENVIRONMENT
            "PYTHONDONTWRITEBYTECODE=1;PYTHONPATH=;ISAACSIM_TEST_RESOURCE_ROOT=${resource_root}"
        ENVIRONMENT_MODIFICATION
            "${python_test_environment_modifications}"
        LABELS "unit;python;${ARG_MODULE};${ARG_LABELS}"
    )
    if(ARG_TIMEOUT)
        set_property(TEST ${test_name} PROPERTY TIMEOUT "${ARG_TIMEOUT}")
    endif()
endfunction()

function(_isaacsim_add_binary_boundary_test module_target)
    if(NOT BUILD_TESTING)
        return()
    endif()
    get_target_property(module_name ${module_target} ISAACSIM_MODULE_NAME)
    add_test(NAME "tests-binary-boundary-${module_target}"
        COMMAND ${CMAKE_COMMAND}
            "-DBINARY=$<TARGET_FILE:${module_target}>"
            -P "${_ISAACSIM_CMAKE_HELPER_DIR}/CheckBinaryDependencies.cmake"
    )
    set_tests_properties("tests-binary-boundary-${module_target}" PROPERTIES
        LABELS "boundary;binary;${module_name}"
    )
endfunction()

function(_isaacsim_get_group_dependency_closure group_name output)
    get_property(group_dependencies GLOBAL PROPERTY "ISAACSIM_GROUP_${group_name}_DEPENDENCIES")
    set(closure)
    foreach(dependency IN LISTS group_dependencies)
        _isaacsim_get_group_dependency_closure("${dependency}" dependency_closure)
        list(APPEND closure ${dependency_closure})
    endforeach()
    list(APPEND closure "${group_name}")
    list(REMOVE_DUPLICATES closure)
    set(${output} "${closure}" PARENT_SCOPE)
endfunction()

function(_isaacsim_generate_install_contract output_dir package_group)
    get_property(module_targets GLOBAL PROPERTY ISAACSIM_MODULE_TARGETS)
    file(MAKE_DIRECTORY "${output_dir}")
    get_property(package_targets GLOBAL PROPERTY "ISAACSIM_GROUP_${package_group}_TARGETS")
    set(INSTALL_CONTRACT_FIND_PACKAGES "")
    if(package_targets)
        set(INSTALL_CONTRACT_FIND_PACKAGES "find_package(${package_group} CONFIG REQUIRED)\n")
    endif()
    set(INSTALL_CONTRACT_TARGET_DECLARATIONS "")
    foreach(module_target IN LISTS module_targets)
        get_target_property(module_group ${module_target} ISAACSIM_GROUP_NAME)
        if(NOT module_group STREQUAL package_group)
            continue()
        endif()
        get_target_property(module_alias ${module_target} ISAACSIM_MODULE_ALIAS)
        get_target_property(c_headers ${module_target} ISAACSIM_PUBLIC_C_HEADERS)
        get_target_property(cpp_headers ${module_target} ISAACSIM_PUBLIC_CPP_HEADERS)
        if(c_headers)
            set(c_sources)
            set(header_index 0)
            foreach(header IN LISTS c_headers)
                math(EXPR header_index "${header_index} + 1")
                set(header_source "header-${module_target}-c-${header_index}.c")
                file(WRITE "${output_dir}/${header_source}" "#include <${header}>\n")
                list(APPEND c_sources "${header_source}")
            endforeach()
            set(consumer_target "isaacsim-consumer-c-${module_target}")
            string(APPEND INSTALL_CONTRACT_TARGET_DECLARATIONS
                "add_executable(${consumer_target} main.c ${c_sources})\n"
                "set_target_properties(${consumer_target} PROPERTIES\n"
                "    C_STANDARD 11\n"
                "    C_STANDARD_REQUIRED ON\n"
                "    C_EXTENSIONS OFF\n"
                ")\n"
                "target_link_libraries(${consumer_target} PRIVATE ${module_alias})\n"
                "if(ISAACSIM_INSTALL_CONTRACT_LOAD_LIBRARIES AND UNIX AND NOT APPLE)\n"
                "    target_link_options(${consumer_target} PRIVATE \"LINKER:--no-as-needed\")\n"
                "endif()\n\n"
            )
        endif()
        if(cpp_headers)
            set(cpp_sources)
            set(header_index 0)
            foreach(header IN LISTS cpp_headers)
                if(header MATCHES "\\.tpp$" OR header MATCHES "(^|/)details/" OR header MATCHES "(^|/)nanobind/")
                    continue()
                endif()
                math(EXPR header_index "${header_index} + 1")
                set(header_source "header-${module_target}-cpp-${header_index}.cpp")
                file(WRITE "${output_dir}/${header_source}" "#include <${header}>\n")
                list(APPEND cpp_sources "${header_source}")
            endforeach()
            set(consumer_target "isaacsim-consumer-cpp-${module_target}")
            string(APPEND INSTALL_CONTRACT_TARGET_DECLARATIONS
                "add_executable(${consumer_target} main.cpp ${cpp_sources})\n"
                "target_compile_features(${consumer_target} PRIVATE cxx_std_17)\n"
                "set_target_properties(${consumer_target} PROPERTIES CXX_EXTENSIONS OFF)\n"
                "target_link_libraries(${consumer_target} PRIVATE ${module_alias})\n"
                "if(ISAACSIM_INSTALL_CONTRACT_LOAD_LIBRARIES AND UNIX AND NOT APPLE)\n"
                "    target_link_options(${consumer_target} PRIVATE \"LINKER:--no-as-needed\")\n"
                "endif()\n\n"
            )
        endif()
        get_target_property(module_type ${module_target} TYPE)
        if(NOT module_type STREQUAL "INTERFACE_LIBRARY")
            _isaacsim_add_binary_boundary_test(${module_target})
        endif()
        get_target_property(symbols_file ${module_target} ISAACSIM_PUBLIC_C_SYMBOLS_FILE)
        if(symbols_file)
            get_target_property(module_name ${module_target} ISAACSIM_MODULE_NAME)
            get_target_property(symbol_prefix ${module_target} ISAACSIM_PUBLIC_C_SYMBOL_PREFIX)
            get_target_property(module_source_dir ${module_target} SOURCE_DIR)
            add_test(NAME "tests-abi-${module_target}"
                COMMAND ${CMAKE_COMMAND}
                    "-DBINARY=$<TARGET_FILE:${module_target}>"
                    "-DSYMBOLS_FILE=${module_source_dir}/${symbols_file}"
                    "-DSYMBOL_PREFIX=${symbol_prefix}"
                    -P "${_ISAACSIM_CMAKE_HELPER_DIR}/CheckExportedSymbols.cmake"
            )
            set_tests_properties("tests-abi-${module_target}" PROPERTIES LABELS "abi;${module_name}")
        endif()
    endforeach()

    configure_file(
        "${ISAACSIM_LIBRARIES_DIR}/testing/install_contract/CMakeLists.txt.in"
        "${output_dir}/CMakeLists.txt"
        @ONLY
    )
    configure_file("${ISAACSIM_LIBRARIES_DIR}/testing/install_contract/main.c.in" "${output_dir}/main.c" @ONLY)
    configure_file("${ISAACSIM_LIBRARIES_DIR}/testing/install_contract/main.cpp.in" "${output_dir}/main.cpp" @ONLY)

    get_property(python_imports GLOBAL PROPERTY "ISAACSIM_GROUP_${package_group}_PYTHON_IMPORTS")
    list(REMOVE_DUPLICATES python_imports)
    set(INSTALL_CONTRACT_PYTHON_MODULES "")
    foreach(python_import IN LISTS python_imports)
        string(APPEND INSTALL_CONTRACT_PYTHON_MODULES "    \"${python_import}\",\n")
    endforeach()
    configure_file(
        "${ISAACSIM_LIBRARIES_DIR}/testing/install_contract/test_python.py.in"
        "${output_dir}/test_python.py"
        @ONLY
    )
endfunction()
